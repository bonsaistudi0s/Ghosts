import net.darkhax.curseforgegradle.TaskPublishCurseForge

import java.util.regex.Pattern

plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev.legacyforge'
    id 'net.darkhax.curseforgegradle' version '1.1.26'
    id "com.modrinth.minotaur" version "2.+"
}

//mixin {
//    add(sourceSets.main, "${mod_id}.refmap.json")
//
//    config("${mod_id}.mixins.json")
//    config("${mod_id}.forge.mixins.json")
//}

legacyForge {
    version = "${minecraft_version}-${forge_version}"

    validateAccessTransformers = true

    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers = ["src/main/resources/META-INF/accesstransformer.cfg"]
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        client {
            client()
        }
        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
        server {
            server()
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    compileOnly project(":common")

    modImplementation "software.bernie.geckolib:geckolib-forge-${minecraft_version}:${geckolib_version}"

    annotationProcessor("org.spongepowered:mixin:0.8.5-SNAPSHOT:processor")
}

jar {
    finalizedBy('reobfJar')
    //manifest.attributes([
    //        "MixinConfigs": "${mod_id}.mixins.json,${mod_id}.forge.mixins.json"
    //])
}

def extractChangelogForVersion = { File file, String version ->
    if (!file || !file.exists()) {
        return null
    }

    def lines = file.getText('UTF-8').readLines()
    if (lines.isEmpty()) return null
    lines[0] = lines[0].replaceFirst('^\\uFEFF', '')

    def lead = '^\\s*#{1,6}\\s*'
    def ver  = Pattern.quote(version)

    def headerPattern = Pattern.compile(lead + '[vV]?' + ver + '\\b.*\\s*$')
    def nextHeaderPattern = Pattern.compile(lead + '[vV]?\\d+\\.\\d+(?:\\.\\d+)?(?:[-+][\\w.]+)?\\b.*\\s*$')

    int start = -1
    for (int i = 0; i < lines.size(); i++) {
        if (headerPattern.matcher(lines[i]).matches()) { start = i + 1; break }
    }

    if (start == -1) {
        return null
    }

    def collected = []
    for (int j = start; j < lines.size(); j++) {
        def line = lines[j]
        if (nextHeaderPattern.matcher(line).matches()) {
            break
        }

        collected << line
    }

    while (!collected.isEmpty() && collected[0].trim().isEmpty()) {
        collected.remove(0)
    }

    while (!collected.isEmpty() && collected[collected.size()-1].trim().isEmpty()) {
        collected.remove(collected.size()-1)
    }

    return collected.isEmpty() ? null : collected.join(System.lineSeparator())
}

def changelogSection = extractChangelogForVersion(project.file('../CHANGELOG.md'), project.property('version').toString())

tasks.register('publishCurseForge', TaskPublishCurseForge) {
    group = 'publishing'
    apiToken = System.getenv("CURSEFORGE_TOKEN")
    def mainFile = upload("602665", reobfJar)

    mainFile.displayName = "[FORGE] Ghosts " + project.property('version').toString() + "-" + minecraft_version

    mainFile.addEnvironment("Client")
    mainFile.addModLoader("Forge")
    mainFile.addGameVersion("1.20")
    mainFile.addGameVersion("1.20.1")

    mainFile.releaseType = "release"

    mainFile.changelog = changelogSection
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "ghost"

    versionName = "[FORGE] Ghosts " + project.property('version').toString() + "-" + minecraft_version

    loaders = ["forge"]
    gameVersions = ["1.20", "1.20.1"]

    uploadFile = reobfJar
    versionNumber = project.property('version').toString()
    versionType = "release"

    changelog = changelogSection
}