import net.darkhax.curseforgegradle.TaskPublishCurseForge

import java.util.regex.Pattern

plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
    id 'net.darkhax.curseforgegradle' version '1.1.26'
    id "com.modrinth.minotaur" version "2.+"
}

dependencies {
    implementation "software.bernie.geckolib:geckolib-neoforge-${minecraft_version}:${geckolib_version}"
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        data {
            data()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

def extractChangelogForVersion = { File file, String version ->
    if (!file || !file.exists()) {
        return null
    }

    def lines = file.getText('UTF-8').readLines()
    if (lines.isEmpty()) return null
    lines[0] = lines[0].replaceFirst('^\\uFEFF', '')

    def lead = '^\\s*#{1,6}\\s*'
    def ver  = Pattern.quote(version)

    def headerPattern = Pattern.compile(lead + '[vV]?' + ver + '\\b.*\\s*$')
    def nextHeaderPattern = Pattern.compile(lead + '[vV]?\\d+\\.\\d+(?:\\.\\d+)?(?:[-+][\\w.]+)?\\b.*\\s*$')

    int start = -1
    for (int i = 0; i < lines.size(); i++) {
        if (headerPattern.matcher(lines[i]).matches()) { start = i + 1; break }
    }

    if (start == -1) {
        return null
    }

    def collected = []
    for (int j = start; j < lines.size(); j++) {
        def line = lines[j]
        if (nextHeaderPattern.matcher(line).matches()) {
            break
        }

        collected << line
    }

    while (!collected.isEmpty() && collected[0].trim().isEmpty()) {
        collected.remove(0)
    }

    while (!collected.isEmpty() && collected[collected.size()-1].trim().isEmpty()) {
        collected.remove(collected.size()-1)
    }

    return collected.isEmpty() ? null : collected.join(System.lineSeparator())
}

def changelogSection = extractChangelogForVersion(project.file('../CHANGELOG.md'), project.property('version').toString())

tasks.register('publishCurseForge', TaskPublishCurseForge) {
    group = 'publishing'
    apiToken = System.getenv("CURSEFORGE_TOKEN")
    def mainFile = upload("602665", jar)

    mainFile.displayName = "[NEOFORGE] Ghosts " + project.property('version').toString() + "-" + minecraft_version

    mainFile.addEnvironment("Client")
    mainFile.addModLoader("NeoForge")
    mainFile.addGameVersion("1.21")
    mainFile.addGameVersion("1.21.1")

    mainFile.releaseType = "release"

    mainFile.changelog = changelogSection
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "ghost"

    versionName = "[NEOFORGE] Ghosts " + project.property('version').toString() + "-" + minecraft_version

    loaders = ["neoforge"]
    gameVersions = ["1.21", "1.21.1"]

    uploadFile = jar
    versionNumber = project.property('version').toString()
    versionType = "release"

    changelog = changelogSection
}